plugins {
    id 'java'
    id 'maven-publish'
}

group 'com.tonglec'
version '1.6.0'
java.sourceCompatibility = JavaVersion.VERSION_11

ext {
    keycloakVersion = '21.1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.keycloak:keycloak-core:$keycloakVersion"
    compileOnly "org.keycloak:keycloak-server-spi:$keycloakVersion"
    compileOnly "org.keycloak:keycloak-server-spi-private:$keycloakVersion"
    compileOnly "org.keycloak:keycloak-services:$keycloakVersion"

    testImplementation "org.keycloak:keycloak-services:$keycloakVersion"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/TongLeC/apple-identity-provider-keycloak")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components["java"])
        }
    }
}

// run 'gradle wrapper' to regenerate gradle/ folder
wrapper {
    gradleVersion = "8.0.2"
    distributionType = Wrapper.DistributionType.BIN
}